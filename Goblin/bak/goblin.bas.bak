100 REM ### MAIN TITLE
105 REM SET BORDER AND BACKG COLORS
110 POKE 53280,2 : POKE 53281,1
120 PRINT "{CLEAR}"
125 PRINT "{DOWN*5}{RIGHT*4}{GREEN}GOBLIN {RED}{REVERSE ON}RELOADED"
130 PRINT "{DOWN*2}{RIGHT*4}{BLUE}DANIELE.OLMISANI@GMAIL.COM"
135 PRINT "{DOWN*2}{RIGHT*4}USE O,P KEYS TO MOVE"
140 PRINT "{DOWN*2}{RIGHT*4}PLEASE WAIT... DEFINING CHARACTERS"
150 REM ### INITIALIZATION
155 REM RECLAIM FREE MEMORY
160 POKE 52,48 : POKE 56,48 : CLR
165 REM CUSTOM CHAR MAP, ORIGINAL CHAR MAP, GAME FIELD START ADDRESSES  
170 CM = 12288 :  OM = 53248 : GF = 54272
175 REM TURN INTERRUPTS OFF
180 POKE 56334,PEEK(56334) AND 254
185 REM SWITCH I/O REGS WITH CHARS ROM
190 POKE 1,PEEK(1) AND 251
205 REM COPY DIGIT CHARS, FROM '0' TO '9'
210 FOR N=384 TO 464 : 
215 : POKE N+CM,PEEK(N+OM)
220 NEXT N
225 REM LOAD CUSTOM CHARS
230 FOR N=0 TO 39
235  : READ A
240  : POKE N+CM,A
245 NEXT N
250 REM ### GAME START
255 IF S>HS THEN HS=S
260 RESTORE
262 GB=0: SF=1 : CR=2 : HF=3 : BL=4 : SP=32
265 Z=1964 : Z1=Z+GF : W=0 : S=J : G=0
270 VS=54296 : AD=54277 : SR=54278 : WF=54276 : LB=GF : HB=54273
320 POKE 1,PEEK(1)OR 4 : REM SWITCH CHARS ROM WITH I/O REGS
330 POKE 56334,PEEK(56334)OR 1 : REM TURN INTERRUPTS ON
340 POKE 53272,(PEEK(53272) AND 240)+12 : REM CUSTOM CHAR SET AT 12288
350 PRINT"{CLEAR}"
360 PRINT"{HOME}{RED}{DOWN*2}{SPACE*17}"HS
370 FOR I=1 TO 118 : REM RANDOM BLS
380  : X=INT(RND(1)*680)+1144
390  : IF PEEK(X)=BL THEN 380
400  : POKE X,BL : REM PUT BL IN POSITION X
410  : POKE X+GF,5 : REM CHANGE COLOR OF POSITION X
420 NEXT I
430 FOR I=1 TO 36
440  : G1=0
450  : X=INT(RND(1)*680)+1144
460  : IF PEEK(X)=BL OR PEEK(X)=SF OR PEEK(X)=HF THEN 450
470  : IF PEEK(X+39)=BL AND PEEK(X+40)=BL AND PEEK(X+41)=BL THEN POKE X,HF : POKE X+GF,0 : G1=1
480  : IF G1=1 THEN G=G+1 : GOTO 500
490  : POKE X,SF : POKE X+GF,8
500 NEXT I
510 POKE Z,32 : Z=Z-40 : Z1=Z1-40 : IF Z<1144 THEN Z=Z+760 : Z1=Z1+760
520 GET A$ : IF A$="O" THEN Z=Z-1 : Z1=Z1-1
530 IF A$="P" THEN Z=Z+1 : Z1=Z1+1
540 IF PEEK(Z)=BL THEN 670
550 IF PEEK(Z)=1 THEN GOSUB 590
560 POKE Z,0 : POKE Z1,0 : FOR T=1 TO 220 : NEXT
570 IF W=36-G THEN J=S : GOSUB 610 : GOTO 250
580 GOTO 510
590 W=W+1 : S=S+25 : PRINT"{HOME}{BLUE}{DOWN*2}"S : POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,17
600 POKE HB,71 : POKELB,12 : FOR T=1 TO 90 : NEXT T : POKE VS,0 : POKE HB,0 :POKE LB,0 : RETURN
610 PRINT"{HOME}{RED}{DOWN*18}{RIGHT*8}{REVERSE OFF}****** ALL RIGHT !******"
620 FOR I=1 TO 10 : GET C$ : NEXT I : REM COLLECT GARBAGE
630 POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,17 : FOR I=1 TO 17
640 H=INT(RND(0)*10)+21 : L=INT(RND(0)*45)+210 : POKE HB,H : POKE LB,L
650 FOR T=1 TO 80 : NEXT T : NEXT I : POKE VS,0 : POKE HB,0 : POKE LB,0
660 RETURN
670 POKE Z,CR : POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,129 : POKE HB,2 : POKE LB,125
680 FOR I=1 TO 400 : NEXT I : POKE VS,15 : POKE HB,0 : POKE LB,0
690 FOR X=1144 TO 1823 : IF PEEK(X)<>1 THEN NEXT X
700 IF PEEK(X)=SF THEN POKE X,HF : NEXT X
710 J=0
720 FOR I=1 TO 10 : GET C$ : NEXT I
730 PRINT"{HOME}{BLUE}{DOWN*20}{REVERSE ON}PLAY AGAIN? (Y/N)" : POKE 646,14
740 GET C$ : IF C$="" THEN GOTO 740
750 IF C$="Y" THEN 250
760 POKE 53272,21 : POKE 53280,14 : POKE 53281,6 : POKE 52,50 : POKE 56,50 : PRINT"{CLEAR)SEE YA!"
1000 DATA 126,219,219,255,165,90,90,165
1100 DATA 60,66,165,129,153,165,66,60
1200 DATA 170,85,170,85,126,219,255,189
1300 DATA 60,66,165,129,165,153,66,60
1400 DATA 170,85,170,85,170,85,170,85