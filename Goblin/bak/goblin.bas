100 REM ### MAIN TITLE
105 REM SET BORDER AND BACKG COLORS
110 POKE 53280,2 : POKE 53281,1
120 PRINT "{CLEAR}"
125 PRINT "{DOWN*5}{RIGHT*4}{GREEN}GOBLIN {RED}{REVERSE ON}RELOADED"
130 PRINT "{DOWN*2}{RIGHT*4}{BLUE}DANIELE.OLMISANI@GMAIL.COM"
135 PRINT "{DOWN*2}{RIGHT*4}USE O,P KEYS TO MOVE"
140 PRINT "{DOWN*2}{RIGHT*4}PLEASE WAIT... DEFINING CHARACTERS"
150 REM ### GAME INITIALIZATION
155 REM RECLAIM MEMORY FOR CUSTOM CHARS
160 POKE 52,48 : POKE 56,48 : CLR
165 REM CUSTOM CHAR MAP, ORIGINAL CHAR MAP, GAME FIELD START ADDRESSES
170 CM = 12288 : OM = 53248 : GF = 54272
175 REM TURN INTERRUPTS OFF
180 POKE 56334,PEEK(56334) AND 254
185 REM SWITCH I/O REGS WITH CHARS ROM
190 POKE 1,PEEK(1) AND 251
205 REM COPY DIGIT CHARS, FROM '0' TO '9'
210 FOR N=384 TO 464 :
215 : POKE N+CM,PEEK(N+OM)
220 NEXT N
225 REM LOAD CUSTOM CHARS
230 FOR N=0 TO 39
235 : READ A
240 : POKE N+CM,A
245 NEXT N
262 GB=0: SF=1 : CR=2 : HF=3 : BL=4 : SP=32
270 VS=54296 : AD=54277 : SR=54278 : WF=54276 : LB=54272 : HB=54273
315 REM SWITCH CHARS ROM WITH I/O REGS
320 POKE 1,PEEK(1)OR 4
325 REM TURN INTERRUPTS ON
330 POKE 56334,PEEK(56334)OR 1
335 REM CUSTOM CHAR SET AT ADDRESS 12288 
340 POKE 53272,(PEEK(53272) AND 240)+12
350 REM ### GAME START
351 IF S>HS THEN HS=S
352 Z=1964 : Z1=Z+GF : W=0 : S=J : G=0 
355 PRINT"{CLEAR}"
360 PRINT"{HOME}{RED}{DOWN*2}{SPACE*17}"HS
370 FOR I=1 TO 118 : REM RANDOM BLOCKS
380 : X=INT(RND(1)*680)+1144
390 : IF PEEK(X)=BL THEN 380
400 : POKE X,BL : POKE X+GF,5
420 NEXT I
425 REM RANDOM FACES
430 FOR I=1 TO 36
440 : F=SF
450 : X=INT(RND(1)*680)+1144
460 : IF PEEK(X)<>SP THEN 450
470 : IF PEEK(X+39)=BL AND PEEK(X+40)=BL AND PEEK(X+41)=BL THEN F=HF : G=G+1
490 : POKE X,F : POKE X+GF,8
500 NEXT I
505 REM ### GAME LOOP
510 POKE Z,32 : Z=Z-40 : Z1=Z1-40 : IF Z<1144 THEN Z=Z+760 : Z1=Z1+760
520 GET A$ : IF A$="O" THEN Z=Z-1 : Z1=Z1-1
530 IF A$="P" THEN Z=Z+1 : Z1=Z1+1
540 IF PEEK(Z)=BL THEN 670
550 IF PEEK(Z)=SF THEN GOSUB 2100
560 POKE Z,0 : POKE Z1,0 : FOR T=1 TO 220 : NEXT
570 IF W=36-G THEN J=S : GOSUB 2200 : GOTO 350
580 GOTO 510
670 GOSUB 2300
750 IF C$="Y" THEN 350
760 POKE 53272,21 : POKE 53280,14 : POKE 53281,6 : POKE 52,50 : POKE 56,50 : PRINT"{CLEAR)SEE YA!"
2100 REM ### EATING ROUTINE
2110 W=W+1 : S=S+25
2120 PRINT"{HOME}{BLUE}{DOWN*2}"S
2130 POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,17
2140 POKE HB,71 : POKE LB,12
2150 FOR T=1 TO 90 : NEXT T
2160 POKE VS,0 : POKE HB,0 :POKE LB,0
2170 RETURN
2200 REM ### GAMEFIELD COMPLETE ROUTINE
2210 PRINT"{HOME}{RED}{DOWN*18}{RIGHT*8}{REVERSE OFF}****** ALL RIGHT !******"
2220 FOR I=1 TO 10 : GET C$ : NEXT I : REM COLLECT GARBAGE
2230 POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,17 : FOR I=1 TO 17
2240 H=INT(RND(0)*10)+21 : L=INT(RND(0)*45)+210 : POKE HB,H : POKE LB,L
2250 FOR T=1 TO 80 : NEXT T : NEXT I : POKE VS,0 : POKE HB,0 : POKE LB,0
2260 RETURN
2300 REM ### GAME END ROUTINE
2310 POKE Z,CR : POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,129 : POKE HB,2 : POKE LB,125
2320 FOR I=1 TO 400 : NEXT I : POKE VS,15 : POKE HB,0 : POKE LB,0
2340 FOR X=1144 TO 1823
2350 : IF PEEK(X)=SF THEN POKE X,HF : POKE X+GF,3
2360 NEXT X
2370 J=0
2380 FOR I=1 TO 10 : GET C$ : NEXT I
2390 PRINT"{HOME}{BLUE}{DOWN*20}{REVERSE ON}PLAY AGAIN? (Y/N)" : POKE 646,14
2395 GET C$ : IF C$="" THEN GOTO 2395
2399 RETURN
9000 REM ### CUSTOM CHARS
9100 DATA 126,219,219,255,165,90,90,165
9200 DATA 60,66,165,129,153,165,66,60
9300 DATA 170,85,170,85,126,219,255,189
9400 DATA 60,66,165,129,165,153,66,60
9500 DATA 170,85,170,85,170,85,170,85